# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Attila Bog√°r <attila.bogar@gmail.com>

_pkgbasename=freetype2
pkgname=lib32-$_pkgbasename-cleartype
pkgver=2.10.1
pkgrel=2
pkgdesc="TrueType font rendering library ClearType patch (32-bit)"
arch=(x86_64)
license=('GPL')
url="https://www.freetype.org/"
# adding harfbuzz for improved OpenType features auto-hinting
# introduces a cycle dep to harfbuzz depending on freetype wanted by upstream
depends=('lib32-zlib' 'lib32-bzip2' 'lib32-libpng' 'lib32-harfbuzz' $_pkgbasename-cleartype)
makedepends=('gcc-multilib')
conflicts=(lib32-$_pkgbasename)
provides=(lib32-$_pkgbasename 'libfreetype.so')
source=(https://download-mirror.savannah.gnu.org/releases/freetype/freetype-${pkgver}.tar.xz{,.sig}
        0001-Enable-table-validation-modules.patch
        0002-Enable-infinality-subpixel-hinting.patch
        0003-Enable-long-PCF-family-names.patch
        0004-Properly-handle-phantom-points-for-variation-fonts-5.patch
        0007-cleartype.patch)
sha1sums=('79874ef4eaa52025126b71d836453b8279bdd331'
          'SKIP'
          '98e5cc125e6f62cd96095eb3ca7834b218030d0c'
          '30951598c78e9f2ee5d79ab167b26973c5968f4a'
          '0cd83a061461cd57eecec4970c87bc6d92c23674'
          'aba1f28bbbf4ff94413096e3d55ac79071a210e2'
          '769ee34c98e78d1e6182476704ddec85f92da394')
validpgpkeys=('58E0C111E39F5408C5D3EC76C1A60EACE707FDA5')

prepare() {
  # Rename source dir to allow building the demos
  mv freetype-${pkgver} freetype2

  cd freetype2
  patch -Np1 -i ../0001-Enable-table-validation-modules.patch
  patch -Np1 -i ../0002-Enable-infinality-subpixel-hinting.patch
  patch -Np1 -i ../0003-Enable-long-PCF-family-names.patch
  # https://bugs.archlinux.org/task/65629
  patch -Np1 -i ../0004-Properly-handle-phantom-points-for-variation-fonts-5.patch
  # Enable ClearType
  patch -Np1 -i ../0007-cleartype.patch
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd freetype2
  ./configure --prefix=/usr --disable-static --libdir=/usr/lib32
  make
}

check() {
  cd freetype2
  make -k check
}

package() {
  cd freetype2
  make DESTDIR="${pkgdir}" install

  rm -r "${pkgdir}"/usr/{include,share}
}

# vim:set ts=2 sw=2 et:
