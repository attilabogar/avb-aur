# Maintainer: Mike Nagie <echo 'cHJvbWlrZTE5ODdAZ21haWwuY29tCg==' | base64 -d>
# Maintainer : speps <speps at aur dot archlinux dot org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Attila Bog√°r <attila.bogar@gmail.com>

pkgname=python2-xcaplib
pkgver=1.2.1
pkgrel=1
pkgdesc="A python library for managing XML documents on XCAP server"
license=('custom:MIT')
arch=('any')
url="http://download.ag-projects.com/XCAP"
depends=('python2-application' 'python2-lxml')
optdepends=('bash-completion: for bash completion')
source=("https://github.com/AGProjects/${pkgname/2}/archive/release-$pkgver.tar.gz")

build() {
  cd "$srcdir/${pkgname/2}-release-$pkgver"
  python2 setup.py build
}

package() {
  cd "$srcdir/${pkgname/2}-release-$pkgver"
  python2 setup.py install --root="$pkgdir" --optimize=1

  #python2 fix
  sed -i "s|^#!.*python$|&2|" `grep -rl "^#!.*python" "$pkgdir"`

  # bash completion
  install -Dm644 bash_completion.d/xcapclient \
    "$pkgdir/usr/share/bash-completion/completions/xcapclient"

  # license
  install -Dm644 LICENSE \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
sha256sums=('7fcc85d700402470cd6129681c4c0b182690e6e1a6602a955a2e48395960d635')
