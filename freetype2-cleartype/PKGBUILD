# Maintainer: Jaden Peterson <jadenpeterson150@gmail.com>
# Contributor: Bruno Pagani (a.k.a. ArchangeGabriel) <bruno.n.pagani@gmail.com>
# Contributor: jeckhack <jeckhack/gmail/com>
# Contributor: Marcin (CTRL) Wieczorek <marcin@marcin.co>
# Contributor: frames <markkuehn at outlook dot com>
# Contributor: Estevao Valadao <estevao@archlinux-br.org>
# Contributor: Tetsumaki <http://goo.gl/YMBdA>
# Contributor: Dave Reisner <d@falconindy.com>
# Contributor: Lee.MaRS <leemars@gmail.com>
# Contributor: freedom
# Contributor: Attila Bog√°r <attila.bogar@gmail.com>

_pkgbasename=freetype2
pkgname=$_pkgbasename-cleartype
pkgver=2.10.1
pkgrel=2
pkgdesc="TrueType font rendering library with ClearType patch"
arch=(x86_64)
license=('GPL')
url="https://www.freetype.org/"
# adding harfbuzz for improved OpenType features auto-hinting
# introduces a cycle dep to harfbuzz depending on freetype wanted by upstream
depends=('zlib' 'bzip2' 'sh' 'libpng' 'harfbuzz')
makedepends=('libx11')
conflicts=($_pkgbasename)
provides=($_pkgbasename 'libfreetype.so')
backup=('etc/profile.d/freetype2.sh')
install=freetype2.install
source=(https://download-mirror.savannah.gnu.org/releases/freetype/freetype-${pkgver}.tar.xz{,.sig}
        0001-Enable-table-validation-modules.patch
        0002-Enable-infinality-subpixel-hinting.patch
        0003-Enable-long-PCF-family-names.patch
        0004-Properly-handle-phantom-points-for-variation-fonts-5.patch
        0007-cleartype.patch
        freetype2.sh)
sha1sums=('79874ef4eaa52025126b71d836453b8279bdd331'
          'SKIP'
          '98e5cc125e6f62cd96095eb3ca7834b218030d0c'
          '30951598c78e9f2ee5d79ab167b26973c5968f4a'
          '0cd83a061461cd57eecec4970c87bc6d92c23674'
          'aba1f28bbbf4ff94413096e3d55ac79071a210e2'
          '769ee34c98e78d1e6182476704ddec85f92da394'
          'bc6df1661c4c33e20f5ce30c2da8ad3c2083665f')
validpgpkeys=('58E0C111E39F5408C5D3EC76C1A60EACE707FDA5')

prepare() {
  cd freetype-${pkgver}
  # Patches from [extra]
  patch -Np1 -i ../0001-Enable-table-validation-modules.patch
  patch -Np1 -i ../0002-Enable-infinality-subpixel-hinting.patch
  patch -Np1 -i ../0003-Enable-long-PCF-family-names.patch
  # https://bugs.archlinux.org/task/65629
  patch -Np1 -i ../0004-Properly-handle-phantom-points-for-variation-fonts-5.patch
  # Enable ClearType
  patch -Np1 -i ../0007-cleartype.patch
}

build() {
  cd freetype-${pkgver}
  ./configure --prefix=/usr --disable-static
  make
}

check() {
  cd freetype-${pkgver}
  make -k check
}

package() {
  cd freetype-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dt "${pkgdir}/etc/profile.d" -m644 ../freetype2.sh
}
